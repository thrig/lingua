#!/usr/bin/env perl
#
# lojban dictionary tool. obtain XML from http://jbovlaste.lojban.org
# and foist that into a sqlite database via `lefanva`
#
# TODO probably needs a rewrite as many new things have been bolted on
# over time

use 5.24.0;
use strictures 2;
use Data::Dumper;
use DBI;
use Getopt::Long qw(GetOptions);
use Term::Size;

# this could perhaps rely on some ENV variable to pick a different
# jbo-to-N database file
my $DBFILE = "$ENV{HOME}/share/lojban/en.db";

# this used to be LIKE by default, but that then required an -e alias,
# so eventually this got changed and the alias removed
my $Flag_Method           = '=';
my $Flag_Searchdefinition = 0;

my $lang = 'jbo';

Getopt::Long::Configure("bundling");
GetOptions(
    'definition|D' => sub { $Flag_Method = 'LIKE'; $Flag_Searchdefinition = 1 },
    'equal|eq|e'   => sub { $Flag_Method = '=' },
    'gismuify|g'   => \my $Flag_Gismu,
    'help|h|?'     => \&emit_help,
    'hify|H'       => \my $Flag_Hify,
    'like|l'       => sub { $Flag_Method = 'LIKE' },
    'natural|n'    => sub { $lang        = 'en' },
    'recurse|R+'   => \my $Flag_Recurse,
    'selmaho|s=s'  => \my $Flag_Selmaho,
    'type|t=s'     => \my $Flag_Type,
    'x'            => \my $Flag_Noexperimental,
) or exit 64;

emit_help() if $Flag_Selmaho and $Flag_Type;

$Flag_Recurse //= 0;

emit_help() if !$Flag_Selmaho and !@ARGV;

my $The_Word = shift;
$The_Word //= '%' if $Flag_Selmaho;

my $Search_Field = 'word';
$Flag_Method = 'LIKE' if $The_Word =~ m/%/;

my $render_by;

if ( $lang eq 'en' ) {
    $render_by    = \&render_en;
    $Flag_Gismu   = 0;
    $Flag_Recurse = 0;
    undef $Flag_Selmaho;
    undef $Flag_Type;
} elsif ( $lang eq 'jbo' ) {
    $render_by = \&render_jbo;
    if ($Flag_Searchdefinition) {
        $Search_Field = 'definition';
    } else {
        # various other lojbanic tools use this convention
        $The_Word =~ tr/h/'/;
        # ignore any unknown gunk (could also error on it) -- [%_] are
        # for sqlite, CLL 3.1 for the other characters
        $The_Word =~ tr/%_',.abcdefgijklmnoprstuvxyz//cd;
    }
}

my @Lo_Valsi;
my $Words_Found = 0;

my $dbh = DBI->connect( 'dbi:SQLite:dbname=' . $DBFILE,
    '', '', { AutoCommit => 1, RaiseError => 1 } );

my $source = $dbh->quote_identifier($lang);

output_setup();

# try an exact lookup
search( $The_Word, $Flag_Recurse );

# if nothing was found (or -g demands such) search for cmavo-compound
# {iicai} or lujvo {brasince} or both {iibrasince} or both with extra
# sprinkles on top {iiicaibrasince}. camxes is another option. mitysisku
# tries to find dictionary words involved with the input; this is a
# different goal than the more complicated art of correctly parsing
# Lojban. in particular, both cmavo and rafsi-for-cmavo will be looked
# up for inputs such as {iicaisince} as mitysisku does not know how to
# tell a cmavo compound {iicai} apart from some (perhaps invalid) lujvo
# involving {carmi}
if (    $lang eq 'jbo'
    and !$Flag_Searchdefinition
    and ( $Words_Found == 0 || $Flag_Gismu ) ) {
    $Flag_Method  = '=';
    $Flag_Recurse = 0;

    # pull cmavo or other matching entries off the beginning of the word
    # TODO need to better match cmavo, this breaks on various cases
#   my $check_entire = 0;
#   while ( length $The_Word ) {
#       my $ret = longest_first_word( $The_Word, $check_entire );
#       $check_entire = 1;
#       if ( defined $ret ) {
#           search( $ret, $Flag_Recurse );
#           $The_Word = substr $The_Word, length $ret;
#       } else {
#           last;
#       }
#   }

    # maybe it is a lujvo?
    if ( length $The_Word ) {
        search( $_, $Flag_Recurse ) for decompose($The_Word);
    }
}

# automatic Lojban definition lookup for an exact English input match
if ( $Words_Found == 1 and $lang eq 'en' ) {
    lineout();
    $lang        = 'jbo';
    $render_by   = \&render_jbo;
    $source      = $dbh->quote_identifier($lang);
    $Flag_Method = '=';
    search( $Lo_Valsi[0], 0 );
}

$dbh->disconnect;
undef $dbh;
emit();
exit $Words_Found == 0 ? 1 : 0;
END { $dbh->disconnect if $dbh }

sub bold { "\e[1m$_[0]\e[0m" }

sub decompose {
    my ($word) = @_;

    # from jbolex.json
    my $gismu_re =
      qr{(?:[bcdfgjklmnprstvxz][aeiou](?:l[bcdfgjkmnprstvxz]|n[bcdfgjklmprstvxz]|r[bcdfgjklmnpstvxz]|m[bcdfgjklnprstvx]|f[cklmnprstx]|p[cfklmnrstx]|t[cfklmnprsx]|b[dgjlmnrvz]|d[bgjlmnrvz]|g[bdjlmnrvz]|k[cflmnprst]|s[fklmnprtx]|v[bdgjlmnrz]|c[fklmnprt]|j[bdglmnrv]|x[flmnprst]|z[bdglmnrv])|(?:c[fklmnprt]|s[fklmnprt]|j[bdgmv]|z[bdgmv]|d[jrz]|t[crs]|b[lr]|f[lr]|g[lr]|k[lr]|m[lr]|p[lr]|v[lr]|x[lr])[aeiou][bcdfgjklmnprstvxz])[aeiou]};

    my @words;

    my $prev_match = 0;
  LOOP: {
        # tail CCV, CVV, or a full gismu. however! cmevla can end with a
        # consonant, so must support parsing those as well; this adds
        # CVC and 4-letter rafsi forms as possible tails, I think
        if ($word =~ m{\G
           ($gismu_re |                                         # lujvo
            # cmevla
            [bcdfgjklmnprstvxz][bcdfgjklmnprstvxz][aeiou][bcdfgjklmnprstvxz] |
            [bcdfgjklmnprstvxz][aeiou][bcdfgjklmnprstvxz][bcdfgjklmnprstvxz] |
            [bcdfgjklmnprstvxz][bcdfgjklmnprstvxz][aeiou] |     # lujvo    
            [bcdfgjklmnprstvxz][aeiou]'?[aeiou] |               # lujvo
            [bcdfgjklmnprstvxz][aeiou][bcdfgjklmnprstvxz] )     # cmevla
            \z
        }cgx
        ) {
            maybe_push_rafsi( \@words, $1 );
            last LOOP;
        }

        # CVC may and CCVC, CVCC must have -y-
        if ($word =~ m{\G
           ([bcdfgjklmnprstvxz][bcdfgjklmnprstvxz][aeiou][bcdfgjklmnprstvxz] |
            [bcdfgjklmnprstvxz][aeiou][bcdfgjklmnprstvxz][bcdfgjklmnprstvxz] |
            [bcdfgjklmnprstvxz][aeiou][bcdfgjklmnprstvxz])
            y
            (?=[bcdfgjklmnprstvxz])
         }cgx
        ) {
            my $rafsi = $1;
            $prev_match += length($rafsi) + 1;
            maybe_push_rafsi( \@words, $rafsi );
            redo LOOP;
        }

        # CVV may have -r- or -n- to join it with next
        if ($word =~ m{\G
           ([bcdfgjklmnprstvxz][aeiou]'?[aeiou])        [rn]
           (?=[bcdfgjklmnprstvxz])
        }cgx
        ) {
            my $rafsi = $1;
            $prev_match += length($rafsi) + 1;
            maybe_push_rafsi( \@words, $rafsi );
            redo LOOP;
        }

        # CVC and CCV can be directly joined; with compound cmavo could
        # have CVV joined without the above [rn] thing
        if ($word =~ m{\G
           ([bcdfgjklmnprstvxz][aeiou][bcdfgjklmnprstvxz] |
           [bcdfgjklmnprstvxz][bcdfgjklmnprstvxz][aeiou]  |
           [bcdfgjklmnprstvxz][aeiou]'?[aeiou])
           (?=[bcdfgjklmnprstvxz])
        }cgx
        ) {
            my $rafsi = $1;
            $prev_match += length($rafsi);
            maybe_push_rafsi( \@words, $rafsi );
            redo LOOP;
        }

        die "mitysisku: unparsable input |$The_Word| (@words)\n",
          " " x ( pos // 0 + $prev_match + 29 ), "^\n";
    }

    die "no words parsed??" unless @words;
    return @words;
}

{
    my ( $cols, $rows, $output, $newlines );

    sub emit {
        if ( $newlines >= $rows and -t *STDOUT ) {
            my $pager = $ENV{PAGER} // 'more';
            open my $pipe, '|-', $pager or die "mitysisku: pager '$pager' failed: $!\n";
            print $pipe $output;
        } else {
            print $output;
        }
    }

    sub lineout {
        my $line = shift // "\n";
        $output .= $line;
        # plus seven as most of the long lines are tab prefixed and tabs
        # are usually eight characters wide when displayed
        $newlines += 1 + int( ( length($line) + 7 ) / $cols );
    }

    sub output_setup {
        if ( -t *STDOUT ) {
            ( $cols, $rows ) = Term::Size::chars(*STDOUT);
        } else {
            ( $cols, $rows ) = ( 80, 24 );
        }
        $output   = '';
        $newlines = 1;
    }
}

sub emit_help {
    print {*STDERR} <<"END_USAGE";
Usage: mitysisku [options] word

Lojban word lookup tool. Options include:

  -e    Use the exact = for the search (default).
  -l    LIKE search ("%word%" unless [%_] found in word)

  -n    Search natural language (English to Lojban) instead of the
        default Lojban to English.

  -g    "Global" search; attempts to break down cmavo-compound and lujvo
        into cmavo, rafsi, and gismu searches. May be called if no
        results were found with a single word search.

  -R    Recurse; also looks up any related words. Repeat flag to
        increase the recursion depth. Recursion is disabled when
        performing a global search.

  -t    Search for words of the given type (e.g. cmavo, ...)
  -s    Search by selmaho.

  -x    Exclude experimental words.

Piping to less(1) may require `less -R` due to the terminal escape
codes used, or that -R is set in the LESS env variable.

END_USAGE
    exit 64;
}

sub gismu_or_cmavo {
    my ($word) = @_;
    if ( length $word == 5 ) {
        # gismu are five letters. mostly.
        return (
            $dbh->selectcol_arrayref( "SELECT word FROM jbo WHERE word = ?", {}, $word ) )
          ->[0];
    }
    my @found;
    if ( length $word == 4 and $word !~ m/'/ ) {
        # truncated gism aka 4-letter rafsi?
        push @found,
          ( $dbh->selectcol_arrayref(
                "SELECT word FROM jbo WHERE word LIKE ?",
                {}, $word . '_'
            )
        )->[0];
    } else {
        # could be a rafsi, or a random cmavo (perhaps incorrectly)
        # glued onto a gismu: the lujvo algorithm does not form
        # {naugleki} from {nanmu gleki} so that probably instead means
        # {nau gleki} of the cmavo {nau}. another problem is {ve'imle};
        # the cmavo {ve'i} does not have a rafsi so one must use {ve'i
        # zei melbi} to lujvo it (CLL 4.6). however, knowing that
        # {ve'imle} is invalid would require a more complicated tool, so
        # instead mitysisku looks up everything relevant and leaves it
        # to the reader to figure out what is what
        push @found,
          (
            $dbh->selectcol_arrayref( "SELECT word FROM jbo WHERE word = ?", {}, $word ) )
          ->[0];

        push @found,
          (
            $dbh->selectcol_arrayref( "SELECT word FROM rafsi WHERE rafsi = ?", {}, $word )
        )->[0];
    }
    return @found;
}

sub hify {
    my ($word) = @_;
    return $word unless $Flag_Hify;
    $word =~ tr/'/h/r;
}

# this is used to pull cmavo-like things off the front of such inputs as
# {ijebo} and such. it gets the order wrong on {iii}, but eh
sub longest_first_word {
    my ($word) = @_;

    my $countq = $dbh->prepare(q{SELECT COUNT(*) FROM jbo WHERE word = ?});

    my @found;
    my $offset = 0;
    my $len    = length $word;
    while ( ++$offset <= $len ) {
        my $search = substr $word, 0, $offset;
        # skip over a leading consonant (but not CC nor C/C which would
        # indicate a brivla that hopefully decompose() can decompose)
        next if $search =~ m/^[bcdfgjklmnprstvxz]$/;
        $countq->execute($search);
        my $count = ( $countq->fetchrow_array )[0];
        last if $count == 0;
        push @found, $search;
    }
    return @found ? $found[-1] : ();
}

# actually also cmavo, sometimes
sub maybe_push_rafsi {
    my ( $aref, $rafsi ) = @_;
    my @ret = grep defined, gismu_or_cmavo($rafsi);
    die "mitysisku: unknown rafsi |$rafsi| in |$The_Word|\n"
      unless @ret;
    push $aref->@*, @ret;
}

sub render_en {
    my ($row) = @_;

    push @Lo_Valsi, $row->{valsi};

    # include type so can e.g. easily identify gismu e.g. in a search
    # for 'return' found 'klakla' but missed the simpler 'xruti'
    $row->{type} = (
        $dbh->selectcol_arrayref(
            "SELECT type FROM jbo WHERE word=?",
            {}, $row->{valsi}
        )
    )->[0];

    $row->{sense} = $row->{sense} ? ' - ' . $row->{sense} : '';

    lineout($row->{word}
          . $row->{sense} . ' - '
          . bold( hify( $row->{valsi} ) ) . ' ('
          . $row->{type} . ')'
          . "\n" );

    return;
}

sub render_jbo {
    my ($row) = @_;

    my ( @gismu, $rafsi );
    if ( $row->{type} eq 'lujvo' ) {
        eval { @gismu = decompose( $row->{word} ) };
        $rafsi = [];
        for my $g (@gismu) {
            my $r = $dbh->selectcol_arrayref(
                "SELECT rafsi FROM rafsi WHERE word = ? ORDER BY rafsi",
                {}, $g );
            push @$rafsi, $r->@* ? $r->@* : '-';
        }
    } else {
        $rafsi =
          $dbh->selectcol_arrayref(
            "SELECT rafsi FROM rafsi WHERE word = ? ORDER BY rafsi",
            {}, $row->{word} );
    }

    lineout(bold( hify( $row->{word} ) ) . "\t("
          . $row->{type}
          . ( $row->{selmaho} ? ', ' . $row->{selmaho} : '' ) . ")"
          . ( @gismu ? ' ' . join ' ', map { bold($_) } @gismu : '' )
          . "\n" );

    lineout( "\t" . ( $row->{definition} // '(no definition provided)' ) . "\n" );

    $row->{rafsi} = join ' ', map { $_ eq '-' ? '-' : bold($_) } $rafsi->@*
      if $rafsi->@*;

    for my $field (qw/gloss rafsi notes/) {
        lineout( "\t" . uc($field) . " " . $row->{$field} . "\n" )
          if defined $row->{$field};
    }

    my $related =
      $dbh->selectcol_arrayref( "SELECT rel FROM rel WHERE word = ? ORDER BY rel",
        {}, $row->{word} );

    lineout(
        "\tRELAT " . join( ' ', map { bold( hify($_) ) } $related->@* ) . "\n" )
      if $related->@*;

    lineout();

    return $related;
}

sub search {
    my ( $word, $depth ) = @_;

    state %seen;

    my $only = '';
    if ( defined $Flag_Type ) {
        my $meth = $Flag_Type =~ m/[%_]/ ? ' LIKE ' : ' = ';
        $only = 'type' . $meth . $dbh->quote($Flag_Type) . ' AND ';
    } elsif ( defined $Flag_Selmaho ) {
        # selma'o always uses LIKE so can type "bai" instead of "BAI"
        # and otherwise not have to match the awkward to type "upper
        # case, except for the h and a few other things" convention on
        # the word class names
        $only = 'selmaho LIKE ' . $dbh->quote($Flag_Selmaho) . ' AND ';
    }

    my $sth =
      $dbh->prepare(
        "SELECT * FROM $source WHERE $only $Search_Field $Flag_Method ? ORDER BY word");

    if ( $Flag_Method eq 'LIKE' and $word !~ m/[%_]/ ) {
        $word = '%' . $word . '%';
    }
    $sth->execute($word);

    my @more;
    while ( my $row = $sth->fetchrow_hashref ) {
        next
          if $Flag_Noexperimental
          and exists $row->{type}
          and $row->{type} =~ m/experimental/;
        $Words_Found++;
        eval {
            my $related = $render_by->($row);
            if ( $depth > 0 and $related ) {
                for my $another ( $related->@* ) {
                    push @more, $another unless $seen{$another}++;
                }
            }
        };
        if ($@) {
            die "mitysisku: $@\n" . Data::Dumper->Dump( [$row], ['row'] );
        }
    }
    $depth--;
    search( $_, $depth ) for @more;
}
