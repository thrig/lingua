#!/usr/bin/env perl
#
# say-jbo - phrase to lojban audio. needs espeak, sox, ffmpeg installed

use 5.26.0;
use warnings;
use File::Spec::Functions qw(catdir catfile);
use Unix::ScriptUtil qw(cd brun);

my $dir = catdir $ENV{HOME}, 'tmp';
cd $dir;

my $bridi = "@ARGV";

# allow ni'o or whatnot to be input as niho
$bridi =~ tr/h/'/;

my $basename = $bridi;
tr/'/h/, s/[^A-Za-z0-9._-]//g, $_ = lc for $basename;

my $wav = $basename . '.wav';
my $nor = $basename . '.nor';
my $mp3 = $basename . '.mp3';

if (!-e $mp3) {
    brun qw(espeak -v other/jbo -w), $wav, $bridi;
    brun qw(sox --norm), $wav, qw(-b 16 -t wav), $nor, qw(rate 44100 dither -s);
    brun qw(ffmpeg -f wav -i), $nor, $mp3;
    unlink $wav, $nor;
}

brun qw(play -q), $mp3;
say catfile($dir, $mp3);
