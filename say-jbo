#!/usr/bin/env perl
#
# say-jbo - espeak wrapper for audioizing lojban phrases

use 5.24.0;
use File::Spec ();
use Text::Unidecode qw(unidecode);

my $bridi    = "@ARGV";
my $filename = $bridi;

# allow ni'o or whatnot to be input as niho
$bridi =~ tr/h/'/;

$filename = lc unidecode($filename);
$filename =~ s/[^a-z0-9_]//g;
$filename = substr $filename, 0, 64;
$filename .= '.wav';

my $wav = File::Spec->catfile( File::Spec->tmpdir, $filename );

my $ret = system qw(espeak -v other/jbo -w), $wav, $bridi;
die "espeak failed: $?\n" if $ret != 0;

# TWEAK try afplay on Mac OS X
# TODO is there a better player on OpenBSD
exec qw(ffplay -autoexit -loglevel quiet -nodisp -hide_banner), $wav;
die "could not exec audio player: $!\n";
